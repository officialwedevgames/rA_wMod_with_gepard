//===== Special Modification =================================
//===== By: ==================================================
//= Ragnarok World
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= eAthena, rAthena, Hercules
//===== Additional Comments: =================================
//= 
//============================================================

//== Stop Post Quest =======================================
morocc_in,142,100,4	script	William	4_M_ORIENT02,{
	mes "[William]";
	mes "Welcome to";
	mes "MacMillan's ^3355FFPost^000000 Workshop.";
	next;
	mes "[William]";
	mes "My family, the MacMillan Clan, has been producing Professional Traffic Signal Posts for more than 250 years. Nowadays, we are booking Special Orders for our unique ornament, ^3355FFStop Post^000000.";
	next;
	switch(select("Could I order one ^3355FFStop Post^000000?:Nah...")) {
	case 1:
		mes "[William]";
		mes "This unique ornament, ^3355FFStop Post^000000, is a traffic signal on the road, and doubles as a hair ornament! This, we solemnly promise on the strength of a 100-year guarantee.";
		next;
		mes "[William]";
		mes "To produce a ^3355FFStop Post^000000, we need ^3355FF91100 Zeny^000000, ^3355FF50 Trunk^000000 and ^3355FF1 Black Dyestuffs^000000.";
		next;
		if ((countitem(Wooden_Block) > 49) && (countitem(Black_Dyestuffs) > 0) && (Zeny > 91099)) {
			delitem Wooden_Block,50;
			delitem Black_Dyestuffs,1;
			Zeny -= 91100;
			mes "[William]";
			mes "Here you are~!";
			mes "This ^3355FFStop Post^000000 has";
			mes "been especially made";
			mes "just for you!";
			mes "Thank you for stopping by!";
			mes "...Get the joke?";
			getitem Stop_Post,1; // Stop_Post
			close;
		}
		else {
			mes "[William]";
			mes "So...";
			mes "When you get those items, swing on by and we'll give you a Stop Post.";
			close;
		}
	case 2:
		mes "[William]";
		mes "Anyway, thank you for coming by 'MacMillan's Workshop.' But think about buying something next time, will ya?";
		close;
	}
}

//== Binoculars Quest ======================================
morocc_in,76,163,4	script	Alchemist	1_M_WIZARD,{
	mes "[Marius]";
	mes "Howdy, new customer!!";
	mes "I know what you're gonna talk about. You want the Magic Glasses from me, riiiiight?";
	next;
	switch(select("What is that?:Manufacture:Ignore him")) {
	case 1:
		mes "[Marius]";
		mes "Hmm~!";
		mes "Well, the official name for them is ^3355FFBinoculars^000000! An optical device that works like a pair of field glasses, they're designed for simultaneous use by both eyes!";
		next;
		mes "[Marius]";
		mes "They're made up of two small telescopes joined with a single focusing device. You can arrange the lenses to produce stereoscopic vision.";
		next;
		mes "[Marius]";
		mes "So?";
		mes "Ain't that";
		mes "something, huh?";
		mes "Muhahahahahaha!";
		mes ". . . . .";
		next;
		mes "[Marius]";
		mes "Hm? You don't seem to";
		mes "believe what I just said?";
		mes "Oh c'mon, jerk. It's real!!";
		next;
		mes "[Marius]";
		mes "I will let you";
		mes "know the items I need...";
		mes "1 ^3355FFGeek Glasses^000000! 100 ^3355FFSteel^000000!";
		mes "And ^3355FF50000 Zeny^000000!";
		close;
	case 2:
		if ((countitem("Spinning_Eyes") > 0) && (countitem("Steel") > 99) && (Zeny > 49999)) {
			mes "[Marius]";
			mes "Perfect, perfect !";
			mes "Now my masterpiece will be complete!";
			mes "Muhahahaha !";
			next;
			delitem "Spinning_Eyes",1;
			delitem "Steel",100;
			Zeny -= 50000;
			mes "[Marius]";
			mes "Here you are!";
			mes "Binoculars !";
			getitem "Binoculars",1; // Binoculars
			next;
			mes "[Marius]";
			mes "Just remember...";
			mes "Don't peep at something you shouldn't look at. Well, at least try not to.";
			close;
		}
		else {
			mes "[Marius]";
			mes "Argggghhhhhhh!!";
			mes "You didn't bring";
			mes "enough items!!!!";
			mes "How dare you";
			mes "disgrace me!!!";
			mes "Baaaadddd !!";
			close;
		}
	case 3:
		mes "[Marius]";
		mes "Hey you~!";
		mes "Now you're in front of Marius, an Alchemist among Alchemists. Ignoring me, eh? Come on, I'll beat your ass!";
		close;
	}
}

