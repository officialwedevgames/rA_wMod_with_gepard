function	script	Start_Quest	{

    if(@loadquestbook) end;

    function BaseLevelRequirements;
    function JobLevelRequirements;
    function ItemRequirements;
    function Comma;

    //- <mission>,<BaseLevel>,<JobLevel>,<Item1>,<Amount1>,<Item2>,<Amount2>
    setarray .mission$[1],
                "1|0|10|909|20|914|20",
                "2|10|0|1052|20|932|20",
                "3|15|0|926|20|957|20",
                "4|20|0|907|20|1019|20",
                "5|25|0|948|20|953|20",
                "6|30|0|901|20|963|20",
                "7|35|0|958|20|912|20",
                "8|40|0|7012|20|1045|20",
                "9|45|0|1042|20|943|20",
                "10|50|0|7009|20|1038|20",
				"11|60|0|1034|20|951|20",
				"12|70|0|950|20|959|20",
				"13|80|0|1041|20|1015|20",
				"14|90|0|1016|20|1012|20",
				"15|99|0|7208|20|7213|20";

    setarray .rewards$[1],
                "1|1000|12208|2|11503|25|11504|25",
                "2|1500|12210|2|32069|10|32070|10",
                "3|2500|12208|2|12215|10|12216|10",
                "4|3000|12210|2|11503|50|11504|50",
                "5|3500|12208|2|12221|5|12211|5",
                "6|4000|12210|2|11503|75|11504|75",
                "7|4500|12208|2|984|10|985|10",
                "8|5000|12210|2|11503|100|11504|100",
                "9|7500|12208|2|32069|10|32070|10",
                "10|10000|12210|2|32065|1|32006|1",
				"11|12500|12208|5|32069|15|32070|15",
				"12|15000|12210|5|12215|15|12216|15",
				"13|15000|12208|5|12221|10|12211|10",
				"14|17500|12210|5|984|20|985|20",
				"15|20000|12208|5|6046|1|32066|1";



    set @loadquestbook, 1;
    
    //- Check the variable
    if(!sq){sq = 1;}

    set @sq_complete, 0;

    explode(.@x$, .mission$[sq], "|");
    
    for(set .@k,0; .@k < getarraysize(.@x$); set .@k,.@k + 1){
        set .@it[.@k], atoi(.@x$[.@k]);
    }

    if(!getarraysize(.@it)){
        dispbottom "[ NO AVAILABLE QUEST ]";
        end;
    }

    mes "[ Beginner Quest #"+sq+" ]";
    mes " ";
    //- Base Level Requirements
    
    if(.@it[1]){
        BaseLevelRequirements(.@it[1]);
        set .@req, .@req + 1;
    }

    //- Job Level Requirements
    if(.@it[2]){
        JobLevelRequirements(.@it[2]);
        set .@req, .@req + 1;
    }

    if(.@it[3]){
        mes " ";
        mes "You assigned to collect the following items listed below.";
        mes " ";
    }

    for(set .@i,3; .@i <= getarraysize(.@it); set .@i,.@i + 2){
        if(.@it[.@i]){
            ItemRequirements(.@it[.@i], .@it[.@i+1]);
            set .@req, .@req + 1;
        }
    }
    
    mes " ";
    mes "[ ^0000ff Rewards ^000000 ]";
    explode(.@a$, .rewards$[sq], "|");
    
    for(set .@p,0; .@p < getarraysize(.@a$); set .@p,.@p + 1){
        set .@re[.@p], atoi(.@a$[.@p]);
    }

    if(.@re[1]){
        mes "* "+Comma(.@re[1],3)+"z";
    }
    
    for(set .@u,2; .@u < getarraysize(.@re); set .@u,.@u + 2){
        if(.@re[2]){
            mes "* ^0000ff"+getitemname(.@re[.@u])+"^000000 x "+.@re[.@u+1]+"";
        }
    }

    if(.@req == @sq_complete){
        next;
        if (select("Claim Rewards") == 1){

            //- Verify if the items are still complete.
            for(set .@i,3; .@i < getarraysize(.@it); set .@i,.@i + 2){
                if(.@it[.@i]){
                    if(countitem(.@it[.@i]) < .@it[.@i+1]){
                        mes "[ Beginner Quest Book]";
                        mes "Required items are incomplete.";
                        close;
                        break;
                    }
                }
            }
			
			.@mail_sender$ = "Beginner Quest Book";
			.@mail_receiver$ = strcharinfo(0);
			.@mail_receiver_id = getcharid(0);
			.@mail_title$ = "Rewards";
			.@mail_message$ = "Congratulations! You completed the Beginner Quest #"+sq+". [ Your reward is attached. ]";
			
			
            mes "[ Beginner Quest Book]";
            mes "Congratulations you completed the Beginner Quest #"+sq+".";

            //- Delete the items collected by the player.
            for(set .@i,3; .@i < getarraysize(.@it); set .@i,.@i + 2){
                delitem .@it[.@i], .@it[.@i+1];
            }

			query_sql("INSERT INTO `mail` (`send_name`, `dest_name`, `dest_id`, `title`, `message`, `status`, `zeny`, `time`) VALUES ('"+.@mail_sender$+"', '"+.@mail_receiver$+"', "+.@mail_receiver_id+", '"+.@mail_title$+"', '"+.@mail_message$+"', 1, "+.@re[1]+", UNIX_TIMESTAMP(NOW()))");
			query_sql("SELECT CAST(`id` AS SIGNED) FROM `mail` WHERE `send_name` = '"+.@mail_sender$+"' ORDER BY CAST(`id` AS SIGNED) DESC LIMIT 1", .@id);
			
            //- Generates the item reward.
            for(set .@u,2; .@u < getarraysize(.@re); set .@u,.@u + 2){
                if(.@re[2]){
					query_sql("INSERT INTO `mail_attachments` (`id`, `index`, `nameid`, `amount`, `refine`, `attribute`, `identify`, `card0`, `card1`, `card2`, `card3`, `unique_id`, `bound`) VALUES ("+.@id+", "+.@num+", "+.@re[.@u]+", "+.@re[.@u+1]+", '0', '0', '1', '0', '0', '0', '0', '0', '1')");
					set .@num, .@num + 1;
				}
            }
			
			announce "Rewards have been sent via mail.",bc_self|bc_blue;
			
            //- Variable for the next level.
            set sq, sq + 1;
        }
        @loadquestbook = 0;
    }
    close2;
		@loadquestbook = 0;
	end;

    function BaseLevelRequirements {
        if(BaseLevel >= getarg(0)){set @sq_complete, @sq_complete + 1;}
        mes "^ff0000*^000000 Reach Base Level "+getarg(0)+" or above.";
        return;
    }

    function JobLevelRequirements {
        if(JobLevel >= getarg(0)){set @sq_complete, @sq_complete + 1;}
        mes "^ff0000*^000000 Reach Job Level "+getarg(0)+" or above.";
        return;
    }

    function ItemRequirements {
        if(countitem(getarg(0)) >= getarg(1)){
            set .@color$ , "^006600";
            set @sq_complete, @sq_complete + 1;
        }else{
            set .@color$ , "^FF0000";
        }
        mes "^0000ff"+getitemname(getarg(0))+"^000000 "+getd(".@color$")+"[ "+countitem(getarg(0))+" / "+getarg(1)+" ]^000000";
    }

    function Comma {
        .@value = getarg(0);
        .@precision = getarg(1,3);
        .@separator$ = getarg( 2,"," );

        .@str$ = ""+.@value;
        .@is_negative = ( .@value < 0 );

        .@length = getstrlen( .@str$ ) - .@precision - .@is_negative;
        while ( .@length > 0 ) {
            .@str$ = insertchar( .@str$, .@separator$ , ( .@length + .@is_negative ) );
            .@length -= .@precision;
        }
        return .@str$;
    }	
}
