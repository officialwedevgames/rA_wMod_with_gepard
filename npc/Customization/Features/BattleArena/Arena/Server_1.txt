

// Waiting Room NPCs
//============================================================
prontera,123,84,3	script	Battle Arena::BA01_MAIN	4_F_JOB_KNIGHT,{

	function OnUpdate;
	
	mes "[ Battle Mart ]";
	mes "Supplies makes the battle complete. Let me open my shop.";
	next;
	switch( select("Usables:Equipments:Exclusive") ){
		case 1:
			mes "[ Battle Mart ]";
			mes "Okay.. great choice.";
			close2;
			callshop "BA_Usables",4;
		end;
		case 2:
			mes "[ Battle Mart ]";
			mes "Okay.. great choice.";
			close2;
			callshop "BA_Equips",4;
		end;	
		case 3:
			mes "[ Battle Mart ]";
			mes "Okay.. great choice.";
			close2;
			callshop "BA_Exclusive",4;
		end;
	}
	end;
	
OnInit:
	set .map$, "bat_b01";
	set .BG_Min, 3;
	set .bonus_badge, 0;
	set .idle_time, 30;	// Seconds
	donpcevent "BA01_MAIN::OnClear";
	waitingroom "Haider Vs Aviator",20,"BA01_MAIN::OnJoin",1;
	end;
	
	
OnInterIfInit:
	//-- Reset Variable
	donpcevent "BA01_MAIN::OnClear";
	end;
	
	
OnCreateTeam:
	set $@BA_TEAM01_ID1, bg_create(.map$,10,290,"BA01_MAIN::OnHaiderQuit","BA01_MAIN::OnHaiderDie");
	set $@BA_TEAM01_ID2, bg_create(.map$,390,10,"BA01_MAIN::OnAviatorQuit","BA01_MAIN::OnAviatorDie");
	end;
	
OnSelectPlayer:
	getwaitingroomusers "BA01_MAIN";
	for(set .@o,0; .@o < getarraysize(.@waitingroom_users); set .@o,.@o+1) {
		attachrid(.@waitingroom_users[.@o]);
			if(checkquest(8511,PLAYTIME) == 0){dispbottom "You are not yet allowed to enter."; end;}
			if(!.@st){
				atcommand "@join #battlearena";
				bg_join $@BA_TEAM01_ID2,.map$,390,10,getcharid(0);
				set .@st, 1;
			}else{
				atcommand "@join #battlearena";
				bg_join $@BA_TEAM01_ID1,.map$,10,290,getcharid(0);
				set .@st, 0;
			}
			set bastatus, 1;
		detachrid;
	}
	end;
	
OnCheckMap:

	bg_get_data($@BA_TEAM01_ID1,1);
	//$@arenamembers
	for(set .@i,0; .@i < getarraysize($@arenamembers); set .@i,.@i+1) {
		attachrid($@arenamembers[.@i]);
			if($@BA01BG == 1 && bastatus && strcharinfo(3) != .map$){
				if(checkquest(8511,PLAYTIME) == 2 || checkquest(8511,PLAYTIME) == 0){
					erasequest 8511;
				}
				setquest 8511;
				bg_leave;
				set bastatus, 0;
				atcommand "@channel leave #battlearena";
			}
		detachrid;
	}

	bg_get_data($@BA_TEAM01_ID2,1);
	//$@arenamembers
	for(set .@k,0; .@k < getarraysize($@arenamembers); set .@k,.@k+1) {
		attachrid($@arenamembers[.@k]);
			if($@BA01BG == 1 && bastatus && strcharinfo(3) != .map$){
				if(checkquest(8511,PLAYTIME) == 2 || checkquest(8511,PLAYTIME) == 0){
					erasequest 8511;
				}
				setquest 8511;
				bg_leave;
				set bastatus, 0;
				atcommand "@channel leave #battlearena";
			}
		detachrid;
	}
	
	sleep 1000;
	goto OnCheckMap;
end;

OnJoinBG:
	getwaitingroomusers "BA01_MAIN";
	for(set .@k,0; .@k < getarraysize(.@waitingroom_users); set .@k,.@k+1) {
		attachrid(.@waitingroom_users[.@k]);
			set .@h, bg_get_data($@BA_TEAM01_ID1,0);
			set .@a, bg_get_data($@BA_TEAM01_ID2,0);
			if(checkquest(8511,PLAYTIME) == 0){dispbottom "You are not yet allowed to enter."; end;}
			if(.@h < .@a || .@h == .@a){
				atcommand "@join #battlearena";
				channel_chat("#battlearena", strcharinfo(0)+" joined the Haider's.");
				bg_join $@BA_TEAM01_ID1,.map$,10,290,getcharid(0);
			}else{
				atcommand "@join #battlearena";
				channel_chat("#battlearena", strcharinfo(0)+" joined the Aviator's.");
				bg_join $@BA_TEAM01_ID2,.map$,390,10,getcharid(0);
			}
			set bastatus, 1;
		detachrid;
	}
	end;
	
OnJoin:
	set .@GetPlayer, getwaitingroomstate(0,"BA01_MAIN");
	
	getwaitingroomusers "BA01_MAIN";
	for(set .@p,0; .@p < getarraysize(.@waitingroom_users); set .@p,.@p+1) {
		attachrid(.@waitingroom_users[.@p]);
			if(checkquest(8511,PLAYTIME) != 0){
				set .@count, .@count + 1;
				set .@invalid, 0;
			}else{
				set .@invalid, 1;
			}
			set .@name$, strcharinfo(0);
		detachrid;
		if(.@invalid){
			chatmes "Player [ "+.@name$+" ] is not yet allowed to join for 1 minute.";
		}
		
	}
	
	if( $@BA01BG == 1 && .@count >= 1){
		donpcevent "BA01_MAIN::OnJoinBG";
	}else if( .@count >= .BG_Min * 2 ){
		set $@BA01BG, 1; // Starting
		donpcevent "BA01_MAIN::OnCreateTeam";
		donpcevent "BA01_MAIN::OnSelectPlayer";
		donpcevent "haider_respawn_warp::OnBGStart";
		donpcevent "avaitor_respawn_warp::OnBGStart";
		donpcevent "BA01_MAIN::OnCheckMap";	
		donpcevent "BA01_MAIN::OnCheckIdle";		
		announce "Battle Arena has started. Proceed to prontera(123,84) to join the match.",bc_all,"0xFFA500";
		initnpctimer;
	}
	end;
	
OnPCKillEvent:
	if( $@BA01BG == 1 && strcharinfo(3) == .map$){
		OnUpdate(getcharid(0),"ADD",1);
	}
end;

	
OnPCLogoutEvent:
	if( $@BA01BG == 1 && strcharinfo(3) == .map$){
		if(checkquest(8511,PLAYTIME) == 2){
			erasequest 8511;
		}
		setquest 8511;
		OnUpdate(getcharid(0),"SUB",50);
		bg_leave;
		set bastatus, 0;
		atcommand "@channel leave #battlearena";
	}
end;

OnHaiderQuit:
	if( $@BA01BG == 1 ){
		if(checkquest(8511,PLAYTIME) == 2){
			erasequest 8511;
		}
		setquest 8511;
		OnUpdate(getcharid(0),"SUB",50);
		bg_leave;
	}
end;

OnHaiderDie:
	if( $@BA01BG == 1 )
	{
		set .Aviator_Score, .Aviator_Score + 1;
		bg_updatescore .map$,.Haider_Score,.Aviator_Score;
		.@r = rand(1,3);
		OnUpdate(getcharid(0),"SUB",.@r);
		sleep2 1000;
		percentheal 100,100;
	}
	end;

OnAviatorQuit:
	if( $@BA01BG == 1 ){
		if(checkquest(8511,PLAYTIME) == 2){
			erasequest 8511;
		}
		setquest 8511;
		OnUpdate(getcharid(0),"SUB",50);
		bg_leave;
	}
end;

OnAviatorDie:
	if( $@BA01BG == 1 )
	{
	
		set .Haider_Score, .Haider_Score + 1;
		bg_updatescore .map$,.Haider_Score,.Aviator_Score;
		.@r = rand(1,3);
		OnUpdate(getcharid(0),"SUB",.@r);
		sleep2 3000;
		percentheal 100,100;
		
	}
	end;

OnTimer1000:
	// Team Members
	set .Haider_Score, 0;
	set .Aviator_Score, 0;
	bg_updatescore .map$,0,0;
	bg_monster $@BA_TEAM01_ID1,.map$,61,150,"Haider Flag",1912,"BA01_MAIN::OnAviatorWin";
	bg_monster $@BA_TEAM01_ID2,.map$,328,150,"Aviator Flag",1913,"BA01_MAIN::OnHaiderWin";
	sleep 3000;
	areapercentheal .map$,2,282,17,297,100,100;
	areawarp .map$,2,282,17,297,.map$,87,75;
	areapercentheal .map$,382,2,397,17,100,100;
	areawarp .map$,382,2,397,17,.map$,311,224;
	end;
	
OnTimer5000:
	mapannounce .map$, "You have 5 minutes to break the enemy flag.",bc_map,"0x00ff00";
	end;

OnTimer15000:
	mapannounce .map$, "Breaking the enemy flag will lead to win and each kills will be 1 point.",bc_map,"0x00ff00";
	end;
	
OnTimer30000:
	mapannounce .map$, "The point system will work only if both the teams didn't defend their side tight.",bc_map,"0x00ff00";
	end;
	
OnTimer45000:
	mapannounce .map$, "Only the team with a highest points will win the match.",bc_map,"0x00ff00";
	end;
	
OnTimer60000:
	mapannounce .map$, "4 minutes remaining to finish the match.",bc_map,"0x00ff00";
	end;
	
OnTimer120000:
	mapannounce .map$, "3 minutes remaining to finish the match.",bc_map,"0x00ff00";
	end;
	
OnTimer180000:
	mapannounce .map$, "2 minutes remaining to finish the match.",bc_map,"0x00ff00";
	end;

OnTimer240000:
	mapannounce .map$, "1 minutes remaining to finish the match.",bc_map,"0x00ff00";
	end;
	
OnTimer270000:
	mapannounce .map$, "30 seconds remaining to finish the match.",bc_map,"0x00ff00";
	end;

OnTimer285000:
	mapannounce .map$, "15 seconds remaining to finish the match.",bc_map,"0x00ff00";
	end;

OnTimer290000:
	mapannounce .map$, "10 seconds remaining to finish the match",bc_map,"0x00ff00";
	end;

OnTimer295000:
	mapannounce .map$, "5 seconds remaining to finish the match",bc_map,"0x00ff00";
	end;

OnTimer300000:
	mapannounce .map$, "The battle is over.",bc_map,"0x00ff00";
	if( .Aviator_Score > .Haider_Score ){
		donpcevent "BA01_MAIN::OnAviatorWin";
	}else if( .Aviator_Score < .Haider_Score ){
		donpcevent "BA01_MAIN::OnHaiderWin";
	}else{ 
		// Draw Game
		bg_get_data($@BA_TEAM01_ID1,1);
		//$@arenamembers
		for(set .@i,0; .@i < getarraysize($@arenamembers); set .@i,.@i+1) {
			attachrid($@arenamembers[.@i]);
			if(strcharinfo(3) == .map$){
				if(vip_status(VIP_STATUS_ACTIVE)){
					set .@bonus, 2;
				}
				getitem 7773, .bonus_badge + 4 + .@bonus;
				OnUpdate(getcharid(0),"DRAW",0);
			}
			if(checkquest(8511,PLAYTIME) <= 2){
				erasequest 8511;
			}
			set bastatus, 0;
			atcommand "@channel leave #battlearena";
			detachrid;
		}
		deletearray $@arenamembers;
		
		bg_get_data($@BA_TEAM01_ID2,1);
		//$@arenamembers
		for(set .@k,0; .@k < getarraysize($@arenamembers); set .@k,.@k+1) {
			attachrid($@arenamembers[.@k]);
			if(strcharinfo(3) == .map$){
				if(vip_status(VIP_STATUS_ACTIVE)){
					set .@bonus, 2;
				}
				getitem 7773, .bonus_badge + 4 + .@bonus;
				OnUpdate(getcharid(0),"DRAW",0);
			}
			if(checkquest(8511,PLAYTIME) <= 2){
				erasequest 8511;
			}
			set bastatus, 0;
			atcommand "@channel leave #battlearena";
			detachrid;
		}
		deletearray $@arenamembers;
		
		set $@BA01BG, 2;
		mapannounce .map$, "The number of Haider Team is "+.Haider_Score+".",bc_map,"0x00ff00";
		mapannounce .map$, "The number of Aviator Team is "+.Aviator_Score+".",bc_map,"0x00ff00";
		mapannounce .map$, "This battle has ended in a draw.",bc_map,"0x00ff00";
		donpcevent "BA01_MAIN::OnClear";
	}
	end;
	
OnCheckIdle:
	if($@BA01BG == 1){
		bg_get_data($@BA_TEAM01_ID1,1);
		//$@arenamembers
		for(set .@i,0; .@i < getarraysize($@arenamembers); set .@i,.@i+1) {
			attachrid($@arenamembers[.@i]);
			
			if(@idle_player >= .idle_time){
				if(checkquest(8511,PLAYTIME) == 2){
					erasequest 8511;
				}
				setquest 8511;
				set @idle_player, 0;
				channel_chat("#battlearena", strcharinfo(0)+" has been kicked for being AFK of "+.idle_time+" seconds.");
				warp "SavePoint",0,0;
				OnUpdate(getcharid(0),"SUB",10);
				atcommand "@channel leave #battlearena";
				bg_leave;
				
			}else{
				if(checkidle()){
					set @idle_player, @idle_player + 1;
				}else{
					set @idle_player, 0;
				}
			}
			
			detachrid;
		}
		deletearray $@arenamembers;
		
		bg_get_data($@BA_TEAM01_ID2,1);
		//$@arenamembers
		for(set .@k,0; .@k < getarraysize($@arenamembers); set .@k,.@k+1) {
			attachrid($@arenamembers[.@k]);
			
			if(@idle_player >= .idle_time){
				if(checkquest(8511,PLAYTIME) == 2){
					erasequest 8511;
				}
				setquest 8511;
				set @idle_player, 0;
				channel_chat("#battlearena", strcharinfo(0)+" has been kicked for being AFK of "+.idle_time+" seconds.");
				warp "SavePoint",0,0;
				OnUpdate(getcharid(0),"SUB",10);
				atcommand "@channel leave #battlearena";
				bg_leave;
			}else{
				if(checkidle()){
					set @idle_player, @idle_player + 1;
				}else{
					set @idle_player, 0;
				}
			}
			
			detachrid;
		}
		deletearray $@arenamembers;
		sleep 1000;
		donpcevent "BA01_MAIN::OnCheckIdle";
	}
	end;
	
OnHaiderWin:
	.@r = rand(5,10);
	bg_get_data($@BA_TEAM01_ID1,1);
	//$@arenamembers
	for(set .@i,0; .@i < getarraysize($@arenamembers); set .@i,.@i+1) {
		attachrid($@arenamembers[.@i]);
		if(strcharinfo(3) == .map$){
			if(vip_status(VIP_STATUS_ACTIVE)){
				set .@bonus, 2;
			}
			getitem 7773, .bonus_badge + 10 + .@bonus;
			OnUpdate(getcharid(0),"WIN",.@r);
		}
		if(checkquest(8511,PLAYTIME) <= 2){
			erasequest 8511;
		}
		set bastatus, 0;
		atcommand "@channel leave #battlearena";
		detachrid;
	}
	deletearray $@arenamembers;
	
	bg_get_data($@BA_TEAM01_ID2,1);
	//$@arenamembers
	for(set .@k,0; .@k < getarraysize($@arenamembers); set .@k,.@k+1) {
		attachrid($@arenamembers[.@k]);
		if(strcharinfo(3) == .map$){
			if(vip_status(VIP_STATUS_ACTIVE)){
				set .@bonus, 2;
			}
			getitem 7773, .bonus_badge + 4 + .@bonus;
			OnUpdate(getcharid(0),"LOSS",.@r);
		}
		if(checkquest(8511,PLAYTIME) <= 2){
			erasequest 8511;
		}
		set bastatus, 0;
		atcommand "@channel leave #battlearena";
		detachrid;
	}
	deletearray $@arenamembers;
	
	set $@BA01BG, 2;
	mapannounce .map$, "Haider wins!",bc_map,"0x00ff00";
	mapannounce .map$, "Congratulations to Haider members.",bc_map,"0x00ff00";
	mapannounce .map$, "Everyone will be moved to the start point.",bc_map,"0x00ff00";
	donpcevent "BA01_MAIN::OnClear";
	end;

OnAviatorWin:
	.@r = rand(5,10);
	bg_get_data($@BA_TEAM01_ID1,1);
	//$@arenamembers
	for(set .@i,0; .@i < getarraysize($@arenamembers); set .@i,.@i+1) {
		attachrid($@arenamembers[.@i]);
		if(strcharinfo(3) == .map$){
			if(vip_status(VIP_STATUS_ACTIVE)){
				set .@bonus, 2;
			}
			getitem 7773, .bonus_badge + 4 + .@bonus;
			OnUpdate(getcharid(0),"LOSS",.@r);
		}
		if(checkquest(8511,PLAYTIME) <= 2){
			erasequest 8511;
		}
		set bastatus, 0;
		atcommand "@channel leave #battlearena";
		detachrid;
	}
	deletearray $@arenamembers;
	
	bg_get_data($@BA_TEAM01_ID2,1);
	//$@arenamembers
	for(set .@k,0; .@k < getarraysize($@arenamembers); set .@k,.@k+1) {
		attachrid($@arenamembers[.@k]);
		if(strcharinfo(3) == .map$){
			if(vip_status(VIP_STATUS_ACTIVE)){
				set .@bonus, 2;
			}
			getitem 7773, .bonus_badge + 10 + .@bonus;
			OnUpdate(getcharid(0),"WIN",.@r);
		}
		if(checkquest(8511,PLAYTIME) <= 2){
			erasequest 8511;
		}
		set bastatus, 0;
		atcommand "@channel leave #battlearena";
		detachrid;
	}
	deletearray $@arenamembers;
	
	set $@BA01BG, 2;
	mapannounce .map$, "Aviator wins!",bc_map,"0x00ff00";
	mapannounce .map$, "Congratulations to Aviator members.",bc_map,"0x00ff00";
	mapannounce .map$, "Everyone will be moved to the start point.",bc_map,"0x00ff00";
	donpcevent "BA01_MAIN::OnClear";
	end;
	
OnClear:
	
	set .Aviator_Score, 0;
	set .Haider_Score, 0;
	bg_destroy $@BA_TEAM01_ID1; 
	set $@BA_TEAM01_ID1, 0;
	bg_destroy $@BA_TEAM01_ID2; 
	set $@BA_TEAM01_ID2, 0;
	mapwarp .map$,"prontera",116,73;
	//maprespawnguildid .map$,0,3; // Just in case someone else
	bg_updatescore .map$,0,0;
	donpcevent "haider_respawn_warp::OnBGStop";
	donpcevent "avaitor_respawn_warp::OnBGStop";
	setnpctimer 0;
	stopnpctimer;
	sleep 2000;
	set $@BA01BG, 0;
	end;
	
	function	OnUpdate	{
		if(getarg(1) == "LOSS"){
			query_sql "UPDATE `pvp_arena_char_stats` SET `loss` = `loss` + 1, `points` = `points` - "+getarg(2)+" WHERE `char_id` = '"+getarg(0)+"'";
		}else if(getarg(1) == "WIN"){
			query_sql "UPDATE `pvp_arena_char_stats` SET `win` = `win` + 1, `points` = `points` + "+getarg(2)+" WHERE `char_id` = '"+getarg(0)+"'";
		}else if(getarg(1) == "DRAW"){
			query_sql "UPDATE `pvp_arena_char_stats` SET `draw` = `draw` + 1, `points` = `points` + "+getarg(2)+" WHERE `char_id` = '"+getarg(0)+"'";
		}else if(getarg(1) == "ADD"){
			query_sql "UPDATE `pvp_arena_char_stats` SET `points` = `points` + "+getarg(2)+" WHERE `char_id` = '"+getarg(0)+"'";
		}else if(getarg(1) == "SUB"){
			query_sql "UPDATE `pvp_arena_char_stats` SET `points` = `points` - "+getarg(2)+" WHERE `char_id` = '"+getarg(0)+"'";
		}
		return;
	}
}

// *********************************************************************

bat_b01,10,290,0	script	haider_respawn_warp	HIDDEN_WARP_NPC,{
	end;
	
OnInit:
	set .map$, "bat_b01";
end;

OnBGStart:
	setnpctimer 0;
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer14000:
	specialeffect 83;
	end;

OnTimer15000:
	areapercentheal .map$,2,282,17,297,100,100;
	areawarp .map$,2,282,17,297,.map$,87,75;
	initnpctimer;
	end;
}

bat_b01,390,10,0	script	avaitor_respawn_warp	HIDDEN_WARP_NPC,{

	end;
OnInit:
	set .map$, "bat_b01";
end;

OnBGStart:
	setnpctimer 0;
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer14000:
	specialeffect 83;
	end;

OnTimer15000:
	areapercentheal .map$,382,2,397,17,100,100;
	areawarp .map$,382,2,397,17,.map$,311,224;
	initnpctimer;
	end;
}

