-	script	Post_Man	FAKE_NPC,{
	
	mes .n$;
	mes "I always provide item deliveries.";
	next;
	switch(select("Event Box","Gift Box","Guild Box","Points Box")) {
	
		case 1:
			set .@mode,1;
			.@type_mode$ = "A";
			query_sql("SELECT `itemid`, `amount`, `refine`, `bound_type`, `rental_time`  FROM `reward_db` WHERE `id_type` = '"+.@type_mode$+"' AND `id_user` = '"+getcharid(3)+"' AND `itemid` != '0' AND `amount` != '0'",.@itemid,.@amount,.@refine,.@bound_type,.@rental_time);
			break;
			
		case 2:
			set .@mode,2;
			.@type_mode$ = "C";
			query_sql("SELECT `itemid`, `amount`, `refine`, `bound_type`, `rental_time`  FROM `reward_db` WHERE `id_type` = '"+.@type_mode$+"' AND `id_user` = '"+getcharid(0)+"' AND `itemid` != '0' AND `amount` != '0'",.@itemid,.@amount,.@refine,.@bound_type,.@rental_time);
			break;
			
		case 3:
			set .@mode,3;
			.@type_mode$ = "G";
			query_sql("SELECT `itemid`, `amount`, `refine`, `bound_type`, `rental_time`  FROM `reward_db` WHERE `id_type` = '"+.@type_mode$+"' AND `id_user` = '"+getcharid(2)+"' AND `itemid` != '0' AND `amount` != '0'",.@itemid,.@amount,.@refine,.@bound_type,.@rental_time);
			break;
			
		case 4:
			set .@mode,4;
			query_sql("SELECT `var`,`name`,`amount`  FROM `reward_db_var` WHERE `account_id` = '"+getcharid(3)+"'",.@var$,.@name$,.@amount);
			break;
			
	}
	
	mes .n$;
	if (.@mode == 4){
			if(!getarraysize(.@var$)){
				mes "^FF0000** ***^000000 NO AVAILABLE POINTS ^FF0000*** **^000000";
				close;
			}
			mes "Select a points.";		
			.@size = getarraysize( .@var$ );
			for( .@i = 0; .@i < .@size; .@i++ ){
				set .@menu$,.@menu$ +"^ff0000"+.@amount[.@i]+"^000000 "+.@name$[.@i]+":";
			}
			.@i = select( .@menu$ ) - 1;
			if((getd(""+getd(".@var$[.@i]")+"") + .@amount[.@i]) > .points_limit){
				mes .n$;
				mes "Can't handle more "+.@name$[.@i]+".";
				close;
			}
			query_sql "DELETE FROM `reward_db_var` WHERE (`account_id` = '"+ getcharid(3) +"' AND `var` = '"+.@var$[.@i]+ "' AND `amount` = '"+.@amount[.@i]+ "') limit 1";
			if(getd(""+.@var$[.@i]+"") == "Zeny"){
				Zeny += .@amount[.@i];
			}else if(getd(""+.@var$[.@i]+"") != "#ROKPOINTS"){
				set .@total, getd(""+.@var$[.@i]+"") + .@amount[.@i];
				set(getd(""+.@var$[.@i]+""), .@total);
			}else{
				set .@total, getd(""+.@var$[.@i]+"") + .@amount[.@i];
				points(getd(""+.@var$[.@i]+""), .@total);
			}
			dispbottom "Gained "+.@amount[.@i]+" "+.@name$[.@i]+"";
			mes "There you go..";
			close;
	}

	if (.@mode == 3 && !getcharid(2)) {		
		mes "Sorry you are not in a guild";
		close;
	}else if(.@mode == 3 && getguildmasterid( getcharid(2) ) != getcharid(0)) {
		mes "Only guild leaders can access this section.";
		close;
	}else if(getarraysize(.@amount) == 0){
		mes "^FF0000** ***^000000 NO AVAILABLE ITEMS ^FF0000*** **^000000";
		close;
	}
	mes "Select an item.";
	next;
	.@size = getarraysize( .@itemid );
	for( .@i = 0; .@i < .@size; .@i++ ){
		set .@menu$,.@menu$ +" "+((.@bound_type[.@i] == 2)?"[^0000ffGuild^000000] ":"")+""+((.@refine[.@i] >= 1)?""+"+"+.@refine[.@i]+"":"")+""+getitemname( .@itemid[.@i] ) + " ["+getitemslots(.@itemid[.@i])+"] x " + .@amount[.@i] + ":";
	}
	.@i = select( .@menu$ ) - 1;
	
	if(.@bound_type[.@i] == 2 && !getcharid(2)){
		mes .n$;
		mes "You are not allowed to claim this reward since your are not in a guild.";
		close;
	}else if( checkweight(.@itemid[.@i],.@amount[.@i]) == 0 ){
		mes .n$;
		mes "You can't carry the stuff you want to claim.";
		mes "Try to store an items to your Kafra Storage.";
		close;
	}
	

		if(.@rental_time[.@i] < 1 && .@bound_type[.@i] >= 1){
		
			getitembound2 .@itemid[.@i],.@amount[.@i],1,.@refine[.@i],0,0,0,0,0,.@bound_type[.@i];
		
		}
		else if(.@bound_type[.@i] < 1 && .@rental_time[.@i] >= 1){
		
			rentitem2 .@itemid[.@i],.@amount[.@i],.@rental_time[.@i],.@refine[.@i],0,0,0,0; 
	
		}
		else{
		
			getitem2 .@itemid[.@i],.@amount[.@i],1,.@refine[.@i],0,0,0,0,0;
		
		}
		
		if(.@mode == 1){
				query_sql "DELETE FROM `reward_db` WHERE (`id_user` = '"+ getcharid(3) +"' AND `itemid` = '"+.@itemid[.@i]+ "' AND `amount` = '"+.@amount[.@i]+"') limit 1";
		}else if(.@mode == 2){
				query_sql "DELETE FROM `reward_db` WHERE (`id_user` = '"+ getcharid(0) +"' AND `itemid` = '"+.@itemid[.@i]+ "' AND `amount` = '"+.@amount[.@i]+"') limit 1";
		}else if(.@mode == 3){
				query_sql "DELETE FROM `reward_db` WHERE (`id_user` = '"+ getcharid(2) +"' AND `itemid` = '"+.@itemid[.@i]+ "' AND `amount` = '"+.@amount[.@i]+"') limit 1";
		}
		mes .n$;
		mes "Here's your "+((.@refine[.@i] >= 1)?""+"+"+.@refine[.@i]+"":"")+" "+getitemname(.@itemid[.@i])+" x "+.@amount[.@i]+".";
		close;
		
OnInit:
	.n$ = "[ ^0000FFPost Man^000000 ]";					// NPC Name
	.points_limit = 1000000000;
	while(1){
		.@date$ = gettimestr("%Y-%m-%d %H:%M:%S", 21);
		query_sql("DELETE FROM `reward_db` WHERE expire_date <= '"+.@date$+"'");
		query_sql("DELETE FROM `reward_db_var` WHERE expire_date <= '"+.@date$+"'");
		sleep 500;
	}
end;

}
		
alberta,148,56,3	duplicate(Post_Man)	Post Man#alb	4W_M_02
//payon,165,98,4	duplicate(Post_Man)	Post Man#pay	4W_M_02
//prontera,112,99,5	duplicate(Post_Man)	Post Man#prt	4W_M_02
//geffen,111,63,5	duplicate(Post_Man)	Post Man#gef	4W_M_02

/*
DROP TABLE IF EXISTS `reward_db_var`;
CREATE TABLE  `reward_db_var` (
  `id` int(11) NOT NULL auto_increment,
  `account_id` int(11) NOT NULL default '0',
  `var` enum('Zeny','#ROKPOINTS','#VOTEPOINTS','#FREEPOINTS','#SHOPPOINTS') NOT NULL default 'Zeny',
  `name` enum('Zeny','Rok Points','Vote Points','Free Points','Shop Points') NOT NULL default 'Zeny',
  `amount` int(11) NOT NULL,
  `expire_date` datetime NOT NULL,
  `date_added` datetime NOT NULL,
  PRIMARY KEY  (`id`),
  KEY `id` (`id`)
) ENGINE=MyISAM AUTO_INCREMENT=1 DEFAULT CHARSET=latin1;


DROP TABLE IF EXISTS `reward_db`;
CREATE TABLE  `reward_db` (
  `id` int(11) NOT NULL auto_increment,
  `id_type` enum('A','C','G') NOT NULL default 'A',
  `id_user` int(11) NOT NULL default '0',
  `itemid` int(10) NOT NULL,
  `amount` int(3) NOT NULL,
  `refine` int(2) NOT NULL default '0',
  `bound_type` enum('0','1','2','3','4') NOT NULL default '0',
  `rental_time` int(5) NOT NULL default '0',
  `expire_date` datetime NOT NULL,
  `date_added` datetime NOT NULL,
  PRIMARY KEY  (`id`),
  KEY `id` (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;

DROP TABLE IF EXISTS `reward_gm_log`;
CREATE TABLE  `reward_gm_log` (
  `id` int(11) NOT NULL auto_increment,
  `id_type` enum('A','C','G') NOT NULL default 'A',
  `id_user` int(11) NOT NULL default '0',
  `itemid` int(10) NOT NULL,
  `amount` int(3) NOT NULL,
  `refine` int(2) NOT NULL default '0',
  `bound_type` enum('0','1','2','3','4') NOT NULL default '0',
  `rental_time` int(5) NOT NULL default '0',
  `expire_date` datetime NOT NULL,
  `date_added` datetime NOT NULL,
  `awarder` char(23) default NULL,
  `reward_type` enum('E','C','D','H','R','W','M','V','O','N','L','B','U','P') NOT NULL default 'E',
  PRIMARY KEY  (`id`),
  KEY `id` (`id`)
) ENGINE=MyISAM DEFAULT CHARSET=latin1;
*/