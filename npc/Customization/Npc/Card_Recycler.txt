
/*
	make sure you loaded the "extra_function.txt" before use this NPC.
*/

-	shop	card_merge_shop	-1,909:100

prontera,105,125,5	script	Card Recycler#card	2_SLOT_MACHINE,{
	if(itemid){
		mes "[ ^ff0000Card Recycler^000000 ]";
		mes "Look like you didn't claim your reward last time. Here's your ^ff0000"+getitemname(itemid)+"^000000 and enjoy!";
		getitem itemid,1;
		set itemid,0;
		close;
	}
do{
	mes "[ ^ff0000Card Recycler^000000 ]";
	mes "This is a machine that converts your luck to a new card that you are not expecting.";
	next;
	.@option = select( "Lets start the merging.","Requirements" );
	if( .@option == 1 ){
		if( Zeny < .zeny_cost ){
			mes "[ ^ff0000Card Recycler^000000 ]";
			mes "You need ^FF0000"+.zeny_cost+" Zeny^000000 per merge.";
			close;
		}
		if( npcshopattach( .shop_npc_name$ ) ){
			deletearray @sold_nameid;
			mes "[ ^ff0000Card Recycler^000000 ]";
			mes "Insert ^FF0000 "+.card_count+" Monster Card^000000 with different names into the window.";
			close2;
			callshop .shop_npc_name$,2;
			end;
		}
	
	}
	else{
		mes "[ ^ff0000Card Recycler^000000 ]";
		mes "You need to insert the cards to the machine.";
		mes " ";
		mes "* Monster Card: ^ff0000"+.card_count+" pc(s)^000000";
		mes "* Zeny: ^ff0000"+.zeny_cost+"^000000";
		mes "* Reward: ^ff0000New Monster Card^000000";
		next;
	}

}while( .@option == 2 );
close;

OnSellItem:
	.@sold_size = getarraysize( @sold_nameid );
	if( .@sold_size > .card_count ){
		mes "[ ^ff0000Card Recycler^000000 ]";
		mes "Look like you inserted more than ^FF0000"+.card_count+" Monster Card^000000.";
		close;
	}else if( .@sold_size < .card_count ){
		mes "[ ^ff0000Card Recycler^000000 ]";
		mes "Look like you inserted less than ^FF0000"+.card_count+" Monster Card^000000.";
		close;
	}else if( .@sold_size == .card_count ){
		mes "[ ^ff0000Card Recycler^000000 ]";
		for( .@i = 0; .@i < .@sold_size; .@i++ ){
			if( compare( .forbidden_card$,"|"+@sold_nameid[.@i]+"|" ) ){
				mes " ~ "+getitemname( @sold_nameid[.@i] )+"^000000 cant be used.";
				close;
			}
			.@item_type[.@i] = getiteminfo( @sold_nameid[.@i],2 );
			.@not_valid = ( .@item_type[.@i] != 6 );
			mes " ~ "+getitemname( @sold_nameid[.@i] )+"^000000 is "+( ( .@not_valid )?"^FF0000NOT":"a" )+" Card^000000.";
			if( .@not_valid ) .@failed++;
		}
		if( !.@failed ){
			mes " ";
			mes "Confirm to exchange these "+.card_count+" Cards into a NEW Card ??";
			next;
			if( select( "YES","NO" ) == 1 ){
				Zeny -= .zeny_cost;
				for( .@i = 0; .@i < .@sold_size; .@i++ )
					delitem @sold_nameid[.@i],1;
				mes "[ ^ff0000Card Recycler^000000 ]";
				mes "Rolling a new card.....";

				.@slow = 0;
				set @cutin$, "";
				set @cc$, "";
				query_sql "SELECT `itemid` FROM `card_merge_db` WHERE `enable` = 1 ORDER BY RAND() LIMIT 50",.@itemid;
				for( .@i = 0; .@i < getarraysize(.@itemid); .@i++ ){
					if( (getarraysize(.@itemid) - .@i) == 20 ){
						mes "^777777-- stopping --^000000";
						.@slow += 200;
					} 
					set @cc$, ""+.@itemid[.@i];
					cutin @cc$,3;
					sleep2 ( .@i + .@slow + 40 );
				}

				for(set .@i,0; .@i < 1; set .@i,.@i + 1){
					set .@chance, rand(1,10000);

					query_sql("SELECT `chance_rate` FROM `card_merge_db` WHERE `enable` = 1 AND `chance_rate` >= '"+.@chance+"' ORDER BY `chance_rate` ASC LIMIT 1", .@rate);
					
					if(.@rate >= 1){
						query_sql("SELECT `itemid` FROM `card_merge_db` WHERE `enable` = 1 AND `chance_rate` = '"+.@rate+"'", .@itemid);
						set .@size, getarraysize(.@itemid);
						if(.@size > 1){  

							set .@rand, rand(1, getarraysize(.@itemid));
							
							query_sql("SELECT `itemid`, `announce` FROM `card_merge_db` WHERE `itemid` = '"+.@itemid[.@rand]+"'", @itemid, .@announce);
							set .@ann, .@announce;
							
							if(.@ann){ 
								announce "Citizens of Midgard,Lady Luck shines upon ["+strcharinfo(0)+"]!! [Card Recycler] has awarded the player with ["+getitemname(@itemid)+"]",0;
							}
							break;
						}else if(.@size == 1){
							
							query_sql("SELECT `itemid`, `announce` FROM `card_merge_db` WHERE `itemid` = '"+.@itemid+"'", @itemid, .@announce);
							set .@ann, .@announce;
							
							if(.@ann){
								announce "Citizens of Midgard,Lady Luck shines upon ["+strcharinfo(0)+"]!! [Card Recycler] has awarded the player with ["+getitemname(@itemid)+"]",0;
							}
							break;
						}
					}
					set .@i, .@i - 1;
					deletearray .@rate;
					deletearray .@itemid;
					deletearray .@announce;
				}
				set @cutin$, ""+@itemid;
				cutin @cutin$,3;
				mes " ";
				mes "You just received a new card: ";
				mes " ~ ^0055FF"+getitemname( @itemid )+"^000000";
				getitem @itemid,1;
				set @itemid, 0;
				close2;
				cutin "",255;
				end;
			}
		}
		
	}
	else{
		mes "[ ^ff0000Card Recycler^000000 ]";
		mes "Make sure you input ^FF0000"+.card_count+" Monster Card^000000 with different names.";
	}
	close2;
	cutin "",255;
	end;

OnInit:
	// how many card requried to change to 1 New card
	.card_count = 2;

	// zeny required for this process.
	.zeny_cost = 25000;

	// list of card that cant be used to exchange/get.
	.forbidden_card$ = "|4142|";
				
	.shop_npc_name$ = "card_merge_shop";
	end;
}



